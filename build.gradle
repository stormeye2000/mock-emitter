plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java-library'
	id 'signing'
	id 'maven-publish'
}

group = 'io.github.stormeye2000'
version = '0.0.1'
sourceCompatibility = '17'

repositories {
	mavenCentral()
	maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation "org.apache.commons:commons-text:1.9"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

java {
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}


////!!!!!!!!!!!!!!!!NOTES
////  MAVEN_USERNAME = sonatype jira username
////  MAVEN_PASSWORD = sonatype jira password
////  GPG_SIGNING_KEY = cli armoured gpg key
////  GPG_SIGNING_KEY_PASSWORD = cli gpg key passphrase
////  gpg --gen-key (with passphrase)
////  gpg --keyserver keyserver.ubuntu.com --send-keys 3310F7C4856ABE3856B2100C1AE0C82DAD2730A9
////  gpg --armor --export-secret-keys carl@stormeye2000.co.uk
///!!!!!!!!!!!!!!!!!!

publishing {
	repositories {
		maven {
			name = 'OSSRH'
			def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username = System.getenv('MAVEN_USERNAME')
				password = System.getenv('MAVEN_PASSWORD')
			}
		}
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/stormeye2000/mock-emitter"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'mock-emitter'
			from components.java
			pom {
				name = 'Mock Emitter'
				packaging = 'jar'
				description = 'test description'
				url = 'https://github.com/stormeye2000/mock-emitter'

				scm {
					connection = 'scm:git:https://github.com/stormeye2000/mock-emitter.git'
					developerConnection = 'git@github.com:stormeye2000/mock-emitter.git'
					url = 'https://github.com/stormeye2000/mock-emitter'
				}

				issueManagement {
					system = 'GitHub'
					url = 'https://github.com/stormeye2000/mock-emitter/issues'
				}

				ciManagement {
					system = 'Github Actions'
					url = 'https://github.com/stormeye2000/mock-emitter/actions'
				}

				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'stormeye2000'
						name = 'Carl Norburn'
						email = 'carl@stormeye.co.uk'
					}
				}
			}
		}
	}



	// Reference at https://docs.gradle.org/current/userguide/signing_plugin.html#sec:in-memory-keys
	signing {
		def signingKey = System.getenv('GPG_SIGNING_KEY') ?: findProperty('GPG_SIGNING_KEY')
		def signingKeyPassword = System.getenv('GPG_SIGNING_KEY_PASSWORD') ?: findProperty('GPG_SIGNING_KEY_PASSWORD')

		useInMemoryPgpKeys(signingKey, signingKeyPassword)
		sign publishing.publications.mavenJava
	}

}